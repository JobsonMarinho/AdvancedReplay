plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    // PaperMC repo
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // ProtocolLib repo
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    // Libby repo
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    implementation project(':legacy')

    compileOnly "io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT"
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.4.0-SNAPSHOT'

    // Dependency resolver at runtime
    implementation 'com.alessiodp.libby:libby-paper:2.0.0-SNAPSHOT'
    // S3 storage backend
    compileOnly 'io.minio:minio:8.6.0'
}

allprojects {
    group = 'me.jumper251.replay'
    version = '1.8.16'

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        sourceCompatibility = 21
        targetCompatibility = 21
    }

    compileJava {
        options.encoding("UTF-8")
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

shadowJar {
    archiveFileName = 'Replay.jar'

    relocate 'com.alessiodp.libby', 'me.jumper251.replay.libs.com.alessiodp.libby'
    relocate 'com.comphenix.packetwrapper', 'me.jumper251.replay.libs.com.comphenix.packetwrapper'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn shadowJar
}